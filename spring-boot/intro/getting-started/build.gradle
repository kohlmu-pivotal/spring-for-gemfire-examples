import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id "java"
	id 'org.springframework.boot' version '3.1.3'
}

description = "Spring GemFire Sample for Getting Started with Spring Boot for VMware GemFire quickly, easily and reliably."

dependencies {

	implementation "com.vmware.gemfire:spring-boot-3.1-gemfire-10.0:1.0.0"
	implementation 'com.vmware.gemfire:gemfire-core:10.0.1'

	implementation "org.assertj:assertj-core:3.24.2"
	implementation "org.springframework.boot:spring-boot-starter-web:3.0.8"

	testImplementation "org.springframework.boot:spring-boot-starter-test:3.0.8"
}

bootRun {
	mainClass = 'dev.gemfire.intro.CrmApplication'
}

processResources {
	eachFile { file ->
		if (!file.name.endsWith(".jks")) {
			file.filter ReplaceTokens, tokens: [
				'project-dir' : rootProject.projectDir.path,
				'project-version' : project.version,
				'samples-dir' : rootProject.projectDir.path + '/spring-gemfire-samples'
			]
		}
	}
}

repositories {
	mavenCentral()
	maven {
		credentials {
			username "$gemfireRepoUsername"
			password "$gemfireRepoPassword"
		}
		url = uri("https://commercial-repo.pivotal.io/data3/gemfire-release-repo/gemfire")
	}
}
