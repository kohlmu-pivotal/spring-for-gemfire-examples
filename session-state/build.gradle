/*
Copyright 2019 - 2021 VMware, Inc.
SPDX-License-Identifier: Apache-2.0
*/

plugins {
    id "org.springframework.boot" version "3.1.9"
    id "io.spring.dependency-management" version "1.1.4"
    id "java"
    id("com.github.node-gradle.node") version "3.5.0"
}

group = "com.vmware.gemfire.session-state-example"
version = "0.0.1-SNAPSHOT"
java {
    toolchain { languageVersion.set(JavaLanguageVersion.of(17)) }
}


configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

bootBuildImage{
    imageName = "docker.io/[YOUR DOCKER USERNAME]/notes-app:${project.version}"
}

repositories {
    mavenCentral()
    maven {
        credentials {
            username "$gemfireRepoUsername"
            password "$gemfireRepoPassword"
        }
        url = uri("https://commercial-repo.pivotal.io/data3/gemfire-release-repo/gemfire")
    }
}


dependencies {
    implementation "com.vmware.gemfire:spring-boot-3.1-gemfire-10.1:1.0.0"
    implementation "com.vmware.gemfire:spring-boot-session-3.1-gemfire-10.1:1.0.0"
    implementation "com.vmware.gemfire:gemfire-core:10.1.0"
    implementation "org.springframework.boot:spring-boot-starter-web"

    testImplementation "org.springframework.boot:spring-boot-starter-test"

}

dependencyManagement {
    dependencies {
        dependencySet(group: "org.apache.logging.log4j", version: "2.16.0") {
            entry("log4j-api")
            entry("log4j-core")
            entry("log4j-to-slf4j")
        }
    }
}

test {
    useJUnitPlatform()
}


node {

    version = "18.12.1"
    npmVersion = "8.19.2"
    download = true

    // Set the work directory for unpacking node
    workDir = file("build/nodejs")

    // Set the work directory for NPM
    npmWorkDir = file("build/node")

    // Set the work directory for Yarn
    yarnWorkDir = file("build/yarn")
}

task appYarnInstall(type: YarnTask) {
    description = "Installs all dependencies from package.json"
    workingDir = file("frontend")
    args = ["install"]
}

task appYarnBuild(type: YarnTask) {
    description = "Builds production version of the frontend"
    workingDir = file("frontend")
    args = ["run", "build"]
}

task copyFrontEnd(type: Copy) {
    from "frontend/build"
    into "build/resources/main/static/"
}


appYarnBuild.dependsOn appYarnInstall
copyFrontEnd.dependsOn appYarnBuild
processResources.dependsOn copyFrontEnd
